/-
The definition of CW complexes follows David W√§rn's suggestion at
https://leanprover.zulipchat.com/#narrow/stream/217875-Is-there-code-for-X.3F/topic/Do.20we.20have.20CW.20complexes.3F/near/231769080
-/

import Mathlib.Topology.ContinuousFunction.Basic
import Mathlib.Topology.Category.TopCat.Basic
import Mathlib.Topology.Category.TopCat.Limits.Products
import Mathlib.Topology.Category.TopCat.Limits.Pullbacks
import Mathlib.Topology.Order
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.Analysis.InnerProductSpace.PiL2 -- EuclideanSpace
import Mathlib.Init.Set

open CategoryTheory


namespace CWComplex
noncomputable section

def Sphere : ‚Ñ§ ‚Üí TopCat
  | Int.ofNat n => TopCat.of <| Metric.sphere (0 : EuclideanSpace ‚Ñù <| Fin <| n + 1) 1
  | _           => TopCat.of Empty

def ClosedBall : ‚Ñ§ ‚Üí TopCat
  | Int.ofNat n => TopCat.of <| Metric.closedBall (0 : EuclideanSpace ‚Ñù <| Fin n) 1
  | _           => TopCat.of Empty

notation:0 "ùïä "n => Sphere n
notation:0 "ùîª "n => ClosedBall n

def SphereInclusion (n : ‚Ñ§) : (ùïä n) ‚Üí (ùîª n + 1) :=
  match n with
  | Int.ofNat _   => fun ‚ü®pt, hpt‚ü© => ‚ü®pt, le_of_eq hpt‚ü©
  | Int.negSucc _ => Empty.rec

theorem continuous_SphereInclusion (n : ‚Ñ§) : Continuous (SphereInclusion n) :=
  match n with
  | Int.ofNat _ => ‚ü®by
      intro _ ‚ü®t, ht, ht'‚ü©
      rw [isOpen_induced_iff]
      use t, ht
      rw [ht'.symm]
      tauto‚ü©
  | Int.negSucc n => ‚ü®by tauto‚ü©

def SigmaSphereInclusion (n : ‚Ñ§) (cells : Type) :
    (Œ£ (_ : cells), ùïä n) ‚Üí (Œ£ (_ : cells), ùîª n + 1) :=
  Sigma.map id fun _ x => SphereInclusion n x

theorem continuous_sigmaSphereInclusion (n : ‚Ñ§) (cells : Type) :
    Continuous (SigmaSphereInclusion n cells) := by
  apply Continuous.sigma_map
  intro _
  apply continuous_SphereInclusion

def BundledSigmaSphereInclusion (n : ‚Ñ§) (cells : Type) :
    ContinuousMap (TopCat.of (Œ£ (_ : cells), ùïä n)) (TopCat.of (Œ£ (_ : cells), ùîª n + 1)) :=
  ‚ü®SigmaSphereInclusion n cells, continuous_sigmaSphereInclusion n cells‚ü©

def SigmaAttachMap (X : TopCat) (n : ‚Ñ§) (cells : Type)
    (attach_maps : cells ‚Üí ContinuousMap (ùïä n) X) :
    (Œ£ (_ : cells), ùïä n) ‚Üí X :=
  fun ‚ü®i, x‚ü© => attach_maps i x

theorem continuous_sigmaAttachMap (X : TopCat) (n : ‚Ñ§) (cells : Type)
    (attach_maps : cells ‚Üí ContinuousMap (ùïä n) X) :
    Continuous (SigmaAttachMap X n cells attach_maps) := by
  apply continuous_sigma
  exact fun i => (attach_maps i).continuous_toFun

def BundledSigmaAttachMap (X : TopCat) (n : ‚Ñ§) (cells : Type)
    (attach_maps : cells ‚Üí ContinuousMap (ùïä n) X) :
    ContinuousMap (TopCat.of (Œ£ (_ : cells), ùïä n)) X :=
  ‚ü®SigmaAttachMap X n cells attach_maps, continuous_sigmaAttachMap X n cells attach_maps‚ü©

-- A type witnessing that X' is obtained from X by attaching n-cells
structure AttachCells (X X' : TopCat) (n : ‚Ñ§) where
  /- The index type over n-cells -/
  cells : Type
  attach_maps : cells ‚Üí ContinuousMap (ùïä n) X
  iso_pushout : X' ‚âÖ Limits.pushout
    (BundledSigmaSphereInclusion n cells)
    (BundledSigmaAttachMap X n cells attach_maps)

end
end CWComplex



structure CWComplex where
  /- Skeleta -/
  sk : ‚Ñ§ ‚Üí TopCat
  /- Every n-skeleton for n < 0 is empty. -/
  sk_neg_empty : ‚àÄ n < 0, sk n = Empty
  /- The (n + 1)-skeleton is obtained from the n-skeleton by attaching (n + 1)-cells. -/
  attach_cells : (n : ‚Ñ§) ‚Üí CWComplex.AttachCells (sk n) (sk (n + 1)) (n + 1)



namespace CWComplex
noncomputable section

-- The inclusion map from X to X', given that X' is obtained from X by attaching n-cells
def AttachCellsInclusion (X X' : TopCat) (n : ‚Ñ§) (att : AttachCells X X' n) : X ‚ü∂ X'
  := @Limits.pushout.inr TopCat _ _ _ X
      (BundledSigmaSphereInclusion n att.cells)
      (BundledSigmaAttachMap X n att.cells att.attach_maps) _ ‚â´ att.iso_pushout.inv

-- The inclusion map from the n-skeleton to the (n+1)-skeleton of a CW-complex
def SkeletaInclusion (X : CWComplex) (n : ‚Ñ§) : X.sk n ‚ü∂ X.sk (n + 1) :=
  AttachCellsInclusion (X.sk n) (X.sk (n + 1)) (n + 1) (X.attach_cells n)

-- The inclusion map from the n-skeleton to the m-skeleton of a CW-complex
def SkeletaInclusion' (X : CWComplex) (n : ‚Ñ§) (m : ‚Ñ§) (n_le_m : n ‚â§ m) :
    X.sk n ‚ü∂ X.sk m :=
  if h : n = m then by
    rw [<- h]
    exact ùüô (X.sk n)
  else by
    have h' : n < m := Int.lt_iff_le_and_ne.mpr ‚ü®n_le_m, h‚ü©
    exact SkeletaInclusion X n ‚â´ SkeletaInclusion' X (n + 1) m h'
  termination_by Int.toNat (m - n)
  decreasing_by
    simp_wf
    rw [Int.toNat_of_nonneg (Int.sub_nonneg_of_le h')]
    linarith

def ColimitDiagram (X : CWComplex) : ‚Ñ§ ‚•§ TopCat where
  obj := X.sk
  map := @fun n m n_le_m => SkeletaInclusion' X n m <| Quiver.Hom.le n_le_m
  map_id := by simp [SkeletaInclusion']
  map_comp := by
    let rec p (n m l : ‚Ñ§) (n_le_m : n ‚â§ m) (m_le_l : m ‚â§ l) (n_le_l : n ‚â§ l) :
        SkeletaInclusion' X n l n_le_l =
        SkeletaInclusion' X n m n_le_m ‚â´
        SkeletaInclusion' X m l m_le_l :=
      if hnm : n = m then by
        unfold SkeletaInclusion'
        aesop
      else by
        have h1 : n < m := Int.lt_iff_le_and_ne.mpr ‚ü®n_le_m, hnm‚ü©
        have h2 : n < l := by linarith
        unfold SkeletaInclusion'
        simp [hnm, Int.ne_of_lt h2]
        rcases em (m = l) with hml | hml
        . aesop
        congr
        rw [p (n + 1) m l h1 m_le_l h2]
        congr
        simp [hml]
        conv => lhs; unfold SkeletaInclusion'
        simp [hml]
      termination_by Int.toNat (l - n)
      decreasing_by
        simp_wf
        rw [Int.toNat_of_nonneg (Int.sub_nonneg_of_le h2)]
        linarith
    intro n m l n_le_m m_le_l
    have n_le_m := Quiver.Hom.le n_le_m
    have m_le_l := Quiver.Hom.le m_le_l
    exact p n m l n_le_m m_le_l (Int.le_trans n_le_m m_le_l)

end
end CWComplex
